# A simple prompt that displays informations in a minimal way
#



_git_prompt_info() {
  if STATUS=$(git status -sb --porcelain=v2) 2>/dev/null; then                                         
    # Branch name
    ref=${STATUS##*branch.head }
    ref=${ref%%
*}

    # Commits ahead/behind
    branch_ab=${STATUS##*\# branch.ab +}
    commits_to_push=${branch_ab%% *}

    commits_to_pull=${branch_ab#*-}
    commits_to_pull=${commits_to_pull%%
*}
    [ $commits_to_push -gt 0 ] 2>/dev/null && commits_to_push=%B%F{cyan}%b$commits_to_push⇑ || commits_to_push=
    [ $commits_to_pull -gt 0 ] 2>/dev/null && commits_to_pull=%B%F{red}%b$commits_to_pull⇓  || commits_to_pull=

    case ${STATUS##*
} in
      \#*) print "$commits_to_pull\
$commits_to_push\
${ZSH_THEME_GIT_PROMPT_CLEAN}$ref";;
      *)
        del=
        ins=
        # Only show +/- when stat not empty
        if ! stat=$(git diff --exit-code --shortstat "$ref" --) ;then
          stat=${stat#*, }
          ins=+${stat%% i*}
          stat=${stat##*, }
          del=-${stat% d*}

          # No deletions                                                                             
          if   [ "${del#*\(}" = "+)" ] ;then; del=
          # Or no insertions
          elif [ "${ins#*\(}" = "-)" ] ;then; ins=
          fi
        fi

        # Parse each status line
        local IFS=$'\n'
        for line in ${=STATUS} ;do
          case $line in
            "? "*|1\ [.A-Z][A-Z]\ *) new=$(( new + 1 )) ;;
          esac
          case $line in
            1\ [A.][A.]\ *) add=$(( add + 1 )) ;;
            1\ [R.][R.]\ *) rename=$(( rename + 1 )) ;;
            1\ [D.][D.]\ *) delete=$(( delete + 1 )) ;;
            1\ [M.][M.]\ *) modify=$(( modify + 1 )) ;;
          esac
        done
            
      print "\
$commits_to_pull\
$commits_to_push\
${ZSH_THEME_GIT_PROMPT_NEW}$new\
${ZSH_THEME_GIT_PROMPT_ADD}$add\
${ZSH_THEME_GIT_PROMPT_RENAME}$rename\
${ZSH_THEME_GIT_PROMPT_DELETE}$delete\
${ZSH_THEME_GIT_PROMPT_MODIFY}$modify\
${ZSH_THEME_GIT_PROMPT_DIRTY}$ref\
${ZSH_THEME_GIT_PROMPT_INS}$ins\
${ZSH_THEME_GIT_PROMPT_DEL}$del";;
    esac
  else
    parentdir=${PWD%/*}
    visible=$(ls -1 | wc -l)
    print "%F{yellow}$visible%F{magenta}$(( $(ls -A1 | wc -l) - visible ))%F{white}${parentdir##*/}"
  fi
}

prompt_setup() {
  prompt_opts=(cr percent sp subst)

  # Git stat
  ZSH_THEME_GIT_PROMPT_NEW='%F{white}'
  ZSH_THEME_GIT_PROMPT_ADD='%F{green}'
  ZSH_THEME_GIT_PROMPT_RENAME='%F{blue}'
  ZSH_THEME_GIT_PROMPT_MODIFY='%F{magenta}'
  ZSH_THEME_GIT_PROMPT_DELETE='%F{red}'
  # Branch
  ZSH_THEME_GIT_PROMPT_DIRTY='%F{yellow}'
  ZSH_THEME_GIT_PROMPT_CLEAN='%F{green}'
  # Git diff
  ZSH_THEME_GIT_PROMPT_INS='%F{green}'
  ZSH_THEME_GIT_PROMPT_DEL='%F{red}'

  # Define prompts.
  PROMPT='%(!:%B%F{red}%m:%B%F{green}%m)%f%b %(?:%F{cyan}:%F{magenta})%c%f '
  RPROMPT='$(_git_prompt_info)%f'
}

prompt_setup "$@"
